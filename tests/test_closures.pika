unittest = import "unittest"

class ClosureTestError: RuntimeError
    function init(msg, modifier)
        super(msg)
        self.modifier = modifier
    end
end

class ReturnValueTestCase: unittest.TestCase
    function testAccumulator()
        {* Literals *}
        
        function accLiteral()
            5
        end
        
        self.assertEquals(accLiteral(), 5, "Test acc for literals")
        
        {* Annonymous Functions *}
        
        function accFunction()
            function()
                "accFunction"
            end
        end
                
        fn = accFunction()
        
        self.assertIs(fn, Function, "Test acc for annonymous functions")
        self.assertEquals(fn(), "accFunction", "Test acc for annonymous functions")
        
        {* Lambda Expressions *}
                        
        function accLambda()
            \(x, y) => x * y
        end
        
        l = accLambda()
        
        self.assertIs(l, Function, "Test acc for lambda expressions")
        self.assertEquals(l(3, 4), 12, "Test acc for lambda expressions")
        
        {* Variables *}
        
        function accParam(a, b, c)
            b
        end
        
        self.assertEquals(accParam(1, 2, 3), 2, "Test acc for variables")
        
        {* Expressions *}
            
        function accExpr(a, b)
            a * b
        end
        
        self.assertEquals(accExpr(2, 3), 6, "Test acc for expressions")
        
        function accCall(a, b)
            accParam(a+1, b+1, a+b)
        end
        
        self.assertEquals(accCall(2, 3), 4, "Test acc for function calls")
        
        {* For loop *}
                
        function accForLoop(n, x)
            for i = 0 to n
                n * x
            end
        end
        
        self.assertEquals(accForLoop(5, 2), 10, "Test acc inside for loops")
    end
end

class ClosureTestCase: unittest.TestCase
    function testSimpleClosure()
        local a, b, c = 'a', 'b', 'c'
        function inner(x, y, z)
            return a..x, b..y, c..z 
        end
        
        ret1, ret2, ret3 = inner('x', 'y', 'z')
        self.assertEquals(ret1, 'ax')
        self.assertEquals(ret2, 'by')
        self.assertEquals(ret3, 'cz')
    end
    
    function testClosureAssignment()
        local a, b, c = 'a', 'b', 'c'
        function inner(x, y, z)
            a, b, c = a..x, b..y, c..z
        end
        inner('x', 'y', 'z')
        self.assertEquals(a, 'ax')
        self.assertEquals(b, 'by')
        self.assertEquals(c, 'cz')
    end
    
    function testLocalsObject()
        local a, b, c = 'a', 'b', 'c'
        function inner(x, y)
            local z, w = 1, 2
            return locals
        end
        
        _locals = inner('x', 'y')
        self.assertEquals(_locals.length,  4) 
        self.assertEquals(_locals.parent.length, 3)
        self.assertEquals(_locals['x'], 'x')
        self.assertEquals(_locals['y'], 'y')
        
        self.assertEquals(_locals.parent['a'], 'a')
        self.assertEquals(_locals.parent['b'], 'b')
        self.assertEquals(_locals.parent['c'], 'c')
        
        self.assertEquals(_locals.closure, inner)
    end
    
    function testClosureSelf()
        function inner()
            return self
        end
        innerSelf = inner()
        self.assertEquals(innerSelf, null)
    end
    
    function testClosureExceptionScope()
        _self = self
        local a, b, c = 'a', 'b', 'c'
        function outer(x, y)
            try
                function inner(z)
                    function modifyScope()
                        a ..= b ..= c ..= z
                    end
                    raise ClosureTestError.new("Inner Exception", modifyScope)
                end
                return inner
            catch e
                return _self.fail("Exception should be handled by the scope that called it.")
            end
        end
        
        self.assertRaises(
            function()
                try
                    inner = outer('x', 'y')
                    inner('z')
                catch e
                    e.modifier()
                    raise
                end
            end,
            ClosureTestError
        )
        
        self.assertEquals(a, "abcz")
        self.assertEquals(b, "bcz")
        self.assertEquals(c, "cz")
    end
    
end